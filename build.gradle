group 'net.oddpoet'
version '0.9.0-SNAPSHOT'

buildscript {
    ext {
        kotlin_version = '1.1.3-2'
        junit_version = '4.12'
        slf4j_version = '1.7.25'
        logback_version = '1.2.3'

        bintray_release_version = '0.5.0'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.novoda:bintray-release:$bintray_release_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.novoda.bintray-release'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    )

    testCompile(
            "org.jetbrains.kotlin:kotlin-test:$kotlin_version",
            "junit:junit:$junit_version",
            "org.slf4j:slf4j-api:$slf4j_version",
            "ch.qos.logback:logback-classic:$logback_version"
    )
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

// ref: https://github.com/novoda/bintray-release/wiki/Configuration-of-the-publish-closure
publish {
    bintrayUser = project.hasProperty("bintray.user") ? project.property("bintray.user")
            : System.getenv("BINTRAY_USER") || "unknown"
    bintrayKey = project.hasProperty("bintray.key") ? project.property("bintray.key")
            : System.getenv("BINTRAY_KEY") || "N/A"
    repoName = 'maven'
    groupId = project.group
    artifactId = project.name
    publishVersion = project.version
    desc = 'rspec style assertion library for kotlin test'
    website = 'https://github.com/odd-poet/kotlin-expect'
    licences = ['Apache-2.0']
    dryRun = false
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.9"
}
jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}
